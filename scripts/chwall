#!/usr/bin/env zsh

set -euo pipefail
trap '' USR1

for cmd in magick ffmpeg matugen hyprctl; do
    command -v "$cmd" >/dev/null || { echo "$cmd missing"; exit 1; }
done

if [[ -z "${1:-}" ]]; then
    echo "usage: $0 /path/to/image" >&2
    exit 1
fi

img_path="$(realpath "$1")"

if [[ ! -f "$img_path" ]]; then
    echo "error: file does not exist -> $img_path"
    exit 1
fi
if ! magick identify "$img_path" >/dev/null 2>&1; then
    echo "error: not a valid image"
    exit 1
fi

: "${XDG_CACHE_HOME:=$HOME/.cache}"
cache_dir="$XDG_CACHE_HOME/arch-rice/wallpaper"
mkdir -p "$cache_dir"

if [[ -f "$cache_dir/current.txt" && "$(<"$cache_dir/current.txt")" == "$img_path" ]]; then
    echo "Wallpaper already active."
    exit 0
fi

echo "$img_path" > "$cache_dir/current.txt"

img_ext="${img_path##*.}"
img_ext="${img_ext:l}"

if [[ "$img_ext" != jpg ]]; then
  magick "$img_path" "$cache_dir/current.jpg"
else
  cp -f -- "$img_path" "$cache_dir/current.jpg"
fi

img_hash=$(sha1sum "$img_path" | cut -d' ' -f1)
img_blurred="$cache_dir/$img_hash.jpg"

# generate only if missing
if [[ ! -f "$img_blurred" ]]; then
    ffmpeg -v error -y -i "$img_path" -vf "scale=iw/2:ih/2,gblur=sigma=15,gblur=sigma=15,gblur=sigma=15" "$img_blurred"
fi

ln -sf "$img_blurred" "$cache_dir/current_blurred.jpg"

# turn off monitor after the script if disabled, as matugen calls hyprctl reload
builtin_off=false
if ! hyprctl monitors -j 2>/dev/null | grep -q '"name": *"eDP-1"'; then
    builtin_off=true
fi

matugen --quiet image "$cache_dir/current.jpg" --mode dark

hyprctl hyprpaper wallpaper ",$cache_dir/current.jpg"

if [[ "$builtin_off" == true ]]; then
    hyprctl keyword monitor "eDP-1,disable" &>/dev/null
fi

if read -q "REPLY?Update SDDM background as well? (requires sudo) [y/N] "; then
    echo
    sudo -v || exit 1
    sudo install -Dm0644 "$cache_dir/current.jpg" "/usr/share/sddm/themes/sequoia/backgrounds/current_wallpaper.jpg"
else
    echo
    echo "Skipping SDDM background update."
fi
